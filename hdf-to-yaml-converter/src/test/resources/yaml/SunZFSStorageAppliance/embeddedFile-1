BEGIN {valid="";chassis="";primarychassis="";typeStart=""}
# Get Chassis
$1 ~ /chassis-[0-9]+/ { chassis = $1}

#Find the Primary Chassis (the one with CPUs)
$1 ~ /cpu-[0-9]+/ {primarychassis = chassis};

# Process instances
valid != "" && NF > 4 && chassis != "" {
    if ( $1 ~ /^chassis/ ) { id = $1}
       else {id = chassis "-" $1}
    name = substr($0,nameStart,nameLength) ; gsub(/ +$/,"",name);
    state = substr($0,stateStart,stateLength) ; gsub(/ +$/,"",state );
    manufacturer = substr($0,manufacturerStart,manufacturerLength ) ; gsub(/ +$/,"",manufacturer );
    model = substr($0,modelStart,modelLength) ; gsub(/ +$/,"",model );
    if (valid=="withoutRPM") {serialLength = length ($0) - serialStart }
       else {if (typeStart == 0){rpm = substr($0,rpmStart) ; gsub(/ +$/,"",rpm ); gsub(/^ +/,"",rpm );} else {rpm = substr($0,rpmStart,rpmLength) ; gsub(/ +$/,"",rpm ); gsub(/^ +/,"",rpm );}}
    serial = substr($0,serialStart,serialLength ) ; gsub(/ +$/,"",serial );
    print "MSHW;" chassis ";" id ";" name ";" state ";" state ";" manufacturer ";" model ";" serial ";" rpm ";"
    }

# Check for header with RPM
$0 ~ / *NAME *STATE *MANUFACTURER *MODEL *SERIAL *RPM */ {
     valid="withRPM"
     nameStart = index($0,"NAME")
     stateStart = index($0,"STATE")
     manufacturerStart = index($0,"MANUFACTURER")
     modelStart = index($0,"MODEL")
     serialStart = index($0,"SERIAL")
     rpmStart = index($0,"RPM")
     typeStart = index($0,"TYPE")
     nameLength = stateStart - nameStart - 1
     stateLength = manufacturerStart - stateStart - 1
     manufacturerLength = modelStart - manufacturerStart - 1
     modelLength = serialStart - modelStart - 1
     serialLength = rpmStart - serialStart - 1
     rpmLength = typeStart - rpmStart - 1
    };

# Check for header without RPM
$0 ~ / *NAME *STATE *MANUFACTURER *MODEL *SERIAL *$/ {
     valid="withoutRPM"
     nameStart = index($0,"NAME")
     stateStart = index($0,"STATE")
     manufacturerStart = index($0,"MANUFACTURER")
     modelStart = index($0,"MODEL")
     serialStart = index($0,"SERIAL")
     nameLength = stateStart - nameStart - 1
     stateLength = manufacturerStart - stateStart - 1
     manufacturerLength = modelStart - manufacturerStart - 1
     modelLength = serialStart - modelStart - 1
     serialLength = rpmStart - serialStart - 1
    };

END {print "MSHW;PrimaryChassis;"primarychassis";"}