BEGIN {DriveCounter = 0}
#CONTROLLER
$1 ~ /MS_HW_CONTROLLER_START/ {ControllerID = $2}
$0 ~ /^ *Controller type *:/ {ControllerType = $4}
$0 ~ /^ *BIOS version *:/ {BIOS = $4}
$0 ~ /^ *Firmware version *:/ {ControllerFirmware = $4}
$1 ~ /^ *Slot/ && $2 ~ /:/ {ControllerSlot = $3}
$1 ~ /MS_HW_CONTROLLER_END/ && ControllerType !- "" {
         print "MSHW_CTRL;" ControllerID ";" ControllerType  ";" BIOS  ";" ControllerFirmware ";" ControllerSlot ";"
         ControllerType = ""
         }

#LOGICAL DISK
$0 ~ /^ *IR volume [0-9]/ { IRVolume = $NF }
$0 ~ /^ *Volume ID *:/ {VolumeID = $NF}
$0 ~ /^ *Status of volume *:/ { VolumeStatus = $NF ; gsub ("[()]","",VolumeStatus) }
$0 ~ /^ *Volume wwid *:/ { VolumeWWID = $NF }
$0 ~ /^ *RAID level *:/ {VolumeRAIDLevel = $NF}
$0 ~ /^ *Size/ && IRVolume != "" {SizeMB = $NF
                       print "MSHW_LD;" IRVolume ";" VolumeID ";" VolumeStatus ";" VolumeWWID ";" VolumeRAIDLevel ";" SizeMB ";" ControllerID ";"
                       IRVolume = ""
                      }

#PHYSICAL DISK
$0 ~ /^ *Device is a/ {DriveCounter = DriveCounter +1 }
$0 ~ /^ *Enclosure # *:/  {Enclosure[DriveCounter] = $NF
                          Controller[DriveCounter] = ControllerID
                         }
$0 ~ /^ *Slot # *:/ {Slot[DriveCounter] = $NF}
$0 ~ /^ *State *:/ {State[DriveCounter] = $NF  ; gsub ("[()]","",State[DriveCounter])}
$0 ~ /^ *Size.*in MB.*in sectors.*:/ {Size[DriveCounter] = $NF ; gsub ("/.*","",Size[DriveCounter]) }
$0 ~ /^ *Manufacturer *:/ {Manufacturer[DriveCounter] = $NF}
$0 ~ /^ *Model Number *:/ {Model[DriveCounter] = $NF}
$0 ~ /^ *Firmware Revision *:/ {Firmware[DriveCounter] = $NF}
$0 ~ /^ *Serial No *:/ {Serial[DriveCounter] = $NF}
$0 ~ /^ *Drive Type *:/ {DriveType[DriveCounter] = $NF}

END { for (I in Enclosure)
      print ("MSHW_PD;Enclosure " Enclosure[I] " Slot " Slot[I] ";" State[I] ";" Size[I] ";" Manufacturer[I] ";" Model[I] ";" Firmware[I] ";" Serial[I] ";" DriveType[I] ";" Controller[I] ";" )
    }