function power(x, n) {
    r = 1;
    #Convert n to integer because if it's a negative value, java considere it as a string  (I think :-) )
    n=n*1
    if (n > 0) { for (i = 0; i < n; i++) { r = r * x; } }
    if (n < 0) { for (i = 0; i > n; i--) { r = r / x; } }
    return r;
}


BEGIN { DeviceID = ""; LowerThreshold = "" ; UpperThreshold = "" ; Voltage = ""; Exponent = ""; OperationalStatus=""; Name = ""; }
($2 == "(voltage-sensor,") { DeviceID = substr($3, 1, length($3) - 1); }
($1 == ":LowWarningThreshold") { LowerThreshold = $2 }
($1 == ":HighWarningThreshold") { UpperThreshold = $2 }
($1 == ":Voltage") { Voltage = $2 }
($1 == ":Exponent") { Exponent = $2 }
($1 == ":OperationalStatus") { OperationalStatus = $2 }
($1 == ":name") {
    Name = $2;

    if (Voltage == "<ERROR:")
    {
        Voltage = "UNKNOWN";
    }
    if (LowerThreshold == "<ERROR:")
    {
        LowerThreshold = "";
    }
    if (UpperThreshold == "<ERROR:")
    {
        UpperThreshold = "";
    }

    if (Exponent ~ /^-?[0-9]+$/)
    {
        if (Voltage != "") { Voltage = Voltage * power(10,Exponent); }
        if (LowerThreshold != "") { LowerThreshold = LowerThreshold * power(10,Exponent); }
        if (UpperThreshold != "") { UpperThreshold = UpperThreshold * power(10,Exponent); }
    }

    if ((DeviceID != "") && (OperationalStatus != "disabled")) {print "MSHW;" DeviceID ";" Name ";" LowerThreshold ";" UpperThreshold ";" Voltage ";"}

    DeviceID = ""; LowerThreshold = "" ; UpperThreshold = "" ; Voltage = ""; Exponent = ""; OperationalStatus=""; Name = "";
}