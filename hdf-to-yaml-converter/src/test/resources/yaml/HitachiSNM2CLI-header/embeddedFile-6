BEGIN {tableValid = ""}
$1 ~ /MS_HW_SYSTEM_START/  { systemName = $2 }
# AMS2100
$0 ~ /LU +Capacity +Size +Group +Pool +Level +Type +Status/ {tableValid = "AMS2100"}
# 9500 / AMS500
$0 ~ /LU +.Mbyte. +C-CTL +D-CTL +Group +Level +Type +Status/ {tableValid = "9500"}
# AMS2300
$0 ~ /LU +Capacity +Size +Group +Pool +Level +Type +of +Paths +Status/ {tableValid = "AMS2300"}
# HUS110
$0 ~ / +LU +Capacity +Size +Group +Pool +Mode +Level +Type +Speed +of +Paths +Status/ {tableValid = "HUS110"}

$1 ~ /MS_HW_SYSTEM_END/  { if (tableValid == "") {print "No valid table format found"}
                                 else {print "Valid table format found for: " systemName " Type: " tableValid  }
                             systemName = "" ; tableValid = ""
                           }

$1 ~ /^[0-9]+$/ && $2 ~ /^[0-9]+/ && tableValid != "" {
    if (tableValid == "AMS2100") {
        LU = $1
        Capacity = $2 ; CapacityGB = Capacity / 1024 ;
        StripeSize = $4 ;
        RAIDGroup = $5
        RaidLevel = $7 $8
        DiskType = $9
        Status = $10
        StatusInformation = $10 " " $11 " " $12 " " $13
        }
    if (tableValid == "HUS110") {
    LU = $1
    Capacity = $2 ;
    if ($3 ~ /[Mm][Bb]/) { CapacityGB = Capacity / 1024 }
    if ($3 ~ /[Tt][Bb]/) { CapacityGB = Capacity * 1024 }
    if ($3 ~ /[Gg][Bb]/) { CapacityGB = Capacity }
    StripeSize = $4 ;
    RAIDGroup = $5
    if ( $9 ~ /^[0-9]/ ) { RaidLevel = $8 $9 ; DiskType = $10 ; Status = $13 }
    else { RaidLevel = $8 ; DiskType = $9 ; Status = $12 }
    if (Status ~ /^[0-9]/) {Status = $14}
    StatusInformation = $14 " " $15 " " $16 " " $17
    }
    if (tableValid == "9500") {
        LU = $1
        Capacity = $2 ; CapacityGB = Capacity / 1024 ;
        StripeSize = ""
        RAIDGroup = $5
        RaidLevel = $6 ; if (RaidLevel ~ /[(]/) {
            RaidLevel = RaidLevel $7
            DiskType = $8
            Status = $9
            StatusInformation = $9 " " $10 " " $11 " " $12
            }
            else {
                DiskType = $7
                Status = $NF
                StatusInformation = $8 " " $9 " " $10 " " $11
                }
        }
    if (tableValid == "AMS2300") {
        LU = $1
        Capacity = $2 ;
        if (Capacity ~ /[Bb]/) {
            if (Capacity ~ /[Mm][Bb]/) { gsub (/[Mm][Bb]/,"",Capacity); CapacityGB = Capacity / 1024 }
            if (Capacity ~ /[Tt][Bb]/) { gsub (/[Tt][Bb]/,"",Capacity); CapacityGB = Capacity * 1024 }
            if (Capacity ~ /[Gg][Bb]/) { gsub (/[Gg][Bb]/,"",Capacity); CapacityGB = Capacity }
            StripeSize = $3 ;
            RAIDGroup = $4
            RaidLevel = $6 $7
            DiskType = $8
            Status = $10
            StatusInformation = $10 " " $11 " " $12 " " $13
            }
        else {
            if ($3 ~ /[Mm][Bb]/) { CapacityGB = Capacity / 1024 }
            if ($3 ~ /[Tt][Bb]/) { CapacityGB = Capacity * 1024 }
            if ($3 ~ /[Gg][Bb]/) { CapacityGB = Capacity }
            StripeSize = $4 ;
            RAIDGroup = $5
            RaidLevel = $7 $8
            DiskType = $9
            Status = $11
            StatusInformation = $11 " " $12 " " $13 " " $14
            }

        }
    DeviceID = systemName ".LU-" LU ; DisplayID = "LU-" LU
# MUST MATCH EF7 (Raid Groups)
# MSHW;systemName;DeviceID;DisplayID;CapacityGB;StripeSize;RAIDGroup;RaidLevel;DiskType;Status;StatusInformation;LogicalDiskType;UseInCapacity;CapacityFreeGB;
    print "MSHW;" systemName";" DeviceID ";" DisplayID ";" CapacityGB ";" StripeSize ";" RAIDGroup ";" RaidLevel ";" DiskType ";" Status ";" StatusInformation ";Volume;0;;"
    }