BEGIN {
    controllerID = "";
}
/^MSHW_Controller / {
    controllerID = $2
}
/^Volume [0-9] is / {
    volumeID = $2
    raidType = $9
    enabled = 0
    diskSize = ""
}
/Volume State:/ {
    if ($0 ~ /[Dd]isabled/)
    {
        enabled = 0
    }
    else
    {
        enabled = 1
    }

    colonIndex = index($0, ":")
    status = substr($0, colonIndex + 3, length - colonIndex - 2)
    commaIndex = index(status, ",")
    if (commaIndex > 1)
    {
        status = substr(status, 1, commaIndex - 1)
    }
}
/Volume Size/ {
    diskSize = $3
    sizeUnit = $4
    if (sizeUnit == "MB,") { diskSize = diskSize * 1048576 }
    else if (sizeUnit == "GB,") { diskSize = diskSize * 1048576 * 1024 }
    else if (sizeUnit == "TB,") { diskSize = diskSize * 1048576 * 1048576 }
}
/^ *$/ {
    if (controllerID != "" && volumeID != "" && enabled == 1)
    {
        print "MSHW;" controllerID ";" volumeID ";" diskSize ";" raidType ";" status
        volumeID = ""
    }
}