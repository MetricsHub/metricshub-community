$1 ~ /</ && $2 ~ /id=/ {
    type=$1
    gsub(/</,"",type)
    ID = $0
    gsub(/.*id="/,"",ID)
    gsub(/">/,"",ID )
    typeArray[ID] = type
    }
$1 ~ /</ && $2 ~ /value=/ {
    field= $1
    gsub(/</,"",field)
    value = $0
    gsub(/.*value="/,"",value)
    gsub(/".>/,"",value)
    fullArray[ID,field] = value
    }
END {
    for (ID in typeArray) {
        enclosureID = ""; speed = ""; status = ""; address = ""; linkstatus = ""; serial = ""; model = ""; firmware = "" ; deviceID = ""; addressType =""; portType =""
        if (tolower(typeArray[ID]) == "fc_port") {
            module = fullArray[ID,"module"] ; split(module,enclosureArray,":") ; enclosureID = enclosureArray[3] ;
            speed = fullArray[ID,"current_rate"] * 1000
            status = fullArray[ID,"status"]
            address = fullArray[ID,"wwpn"]
            linkstatus = fullArray[ID,"port_state"]
            serial = fullArray[ID,"serial"]
            model = fullArray[ID,"model"]
            firmware = fullArray[ID,"active_firmware"]
            deviceID = fullArray[ID,"component_id"]
            addressType = "WWN"
            portType = "FC"
            }
        if (tolower(typeArray[ID]) == "ib_port") {
        # No available output, so guessing
            switch = fullArray[ID,"component_id"]
            split(switch ,enclosureArray,":")
            enclosureID = enclosureArray[1] ":" enclosureArray[2] ":" enclosureArray[3]
            status = fullArray[ID,"status"]
            deviceID = fullArray[ID,"component_id"]
            portType = "IB"
            }
        if (tolower(typeArray[ID]) == "ethernet_port") {
            if ( fullArray[ID,"role"] == "Internal" ) {
                switch = fullArray[ID,"connected_component"]
                split(switch ,enclosureArray,":")
                enclosureID = enclosureArray[1] ":" enclosureArray[2] ":" enclosureArray[3]
                deviceID = fullArray[ID,"connected_component"]
                }
            else if ( fullArray[ID,"role"] == "iSCSI" ) {
                module = fullArray[ID,"module_id"]
                split(module,enclosureArray,":")
                enclosureID = enclosureArray[3]
                deviceID = fullArray[ID,"ip_interface_name"]
                }
            else if ( fullArray[ID,"role"] == "Component" ) {
                module = fullArray[ID,"module_id"]
                split(module,enclosureArray,":")
                enclosureID = enclosureArray[3]
                deviceID = fullArray[ID,"connected_component"]
                }
            else if ( fullArray[ID,"role"] != "NONE" ) {
                module = fullArray[ID,"module_id"]
                split(module,enclosureArray,":")
                enclosureID = enclosureArray[3]
                deviceID = fullArray[ID,"module_id"] ":" fullArray[ID,"role"]
                }
            portType = "Ethernet"
            speed = fullArray[ID,"negotiated_speed_Mbs"]
            linkstatus = fullArray[ID,"is_link_up"]
            }
        if ( deviceID != "" ) {
            print "MSHW;" portType ";" deviceID ";" enclosureID ";" speed ";" status ";" address ";" addressType ";" linkstatus ";" serial ";" model ";Firmware: " firmware ";"
            }
        }
    }