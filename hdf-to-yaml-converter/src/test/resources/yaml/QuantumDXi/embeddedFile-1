# header
$1 ~ /Product/ && $2 ~ /Model/ {Model = $NF}
$1 ~ /Hardware/ && $2 ~ /Configuration/ {Configuration = $NF}
$1 ~ /System/ && $2 ~ /Version/ {SystemVersion = $NF}
$1 ~ /System/ && $2 ~ /Serial/ && $3 ~ /Number/ {SystemSerialNumber = $NF}

# syscli --getstatus storagearray
# SECTION storagearray
$0 ~ /syscli --getstatus storagearray/ {Section = "storagearray"}

Section == "storagearray" && $1 ~ /Array/ && $2 ~ /=/ { ID = $NF ; gsub(/[\]]/,"",ID) ; ID = "Array_" ID
                           Type[ID] = "Enclosure"
                         }
Section == "storagearray" && $1 ~ /Name/ && $2 ~ /=/ { Name[ID] = $0 ; gsub(/^.*= /,"",Name[ID]) }

Section == "storagearray" && $1 ~ /Status/ && $2 ~ /=/ { Status[ID] = $3 ;
                            StatusInformation[ID] = $0 ; gsub(/^.*= /,"",StatusInformation[ID])
                          }

# syscli --getstatus systemboard
# SECTION systemboard
$0 ~ /syscli --getstatus systemboard/ {Section = "systemboard"}

Section == "systemboard" && $1 ~ /Component/ && $2 ~ /=/ { ID = "SysBoard_" $NF ; gsub(/[\]]/,"",ID) }

Section == "systemboard" && $1 ~ /Type/ && $2 ~ /=/ { Type[ID] = $NF }

Section == "systemboard" && $1 ~ /Value/ && $2 ~ /=/ { Value[ID] = $3 }

Section == "systemboard" && $1 ~ /Status/ && $2 ~ /=/ { Status[ID] = $3 ;
                            StatusInformation[ID] = $0 ; gsub(/^.*= /,"",StatusInformation[ID])
                          }
Section == "systemboard" && $1 ~ /Name/ && $2 ~ /=/ { Name[ID] = $0 ; gsub(/^.*= /,"",Name[ID])
#Attaching to Array One
                                          AttachedTo[ID] = Name["Array_1"]
                                        }



# SECTION storagearraydetails
#  for array in `syscli --getstatus storagearray | grep -oP '(?<=Name = )[\w\d]+'`; do echo MSHW_START $array;syscli --getstatus storagearraydetails --name $array ; echo MSHW_END $array; done

$1 ~ /MSHW_START/ { SectionEnclosure = $2 ; Section = "storagearraydetails" }

Section == "storagearraydetails" && $1 ~ /Name/ { SectionType = $3 }

Section == "storagearraydetails" && $1 ~ /subcomponent/ {Section = "storagearraydetails_subcomponent"}



# SECTION storagearraydetails_subcomponent
Section == "storagearraydetails_subcomponent" && $1 ~ /[\[]component/ {Section = "storagearraydetails"}

Section == "storagearraydetails_subcomponent" && $1 ~ /subcomponent/ && SectionEnclosure != "" {
    ID = SectionEnclosure "_" SectionType "_" $NF ; gsub(/[\]]/,"",ID)
    AttachedTo[ID] = SectionEnclosure
    Type[ID] = SectionType
    }

Section == "storagearraydetails_subcomponent" && $1 ~ /Name/ && $2 ~ /=/ {
    Name[ID] = $0 ; gsub(/^.*= /,"",Name[ID]) ;
    }

Section == "storagearraydetails_subcomponent" && $1 ~ /Location/ && $2 ~ /=/ {
    Location[ID] = $0 ; gsub(/^.*= /,"",Location[ID])
    }

Section == "storagearraydetails_subcomponent" && $1 ~ /Value/ && $2 ~ /=/ { Value[ID] = $3 }

Section == "storagearraydetails_subcomponent" && $1 ~ /Status/ && $2 ~ /=/ { Status[ID] = $3 ;
                            StatusInformation[ID] = $0 ; gsub(/^.*= /,"",StatusInformation[ID])
                          }

# syscli --getstatus hostbusadapter
# SECTION hostbusadapter
$0 ~ /syscli --getstatus hostbusadapter/ {Section = "hostbusadapter"}

Section == "hostbusadapter" && $1 ~ /Hostbus/ && $2 ~ Adapter && $3 ~ /=/ { ID = "HBA_" $NF ; gsub(/[\]]/,"",ID) ; Type[ID] = "FCC"}

Section == "hostbusadapter" && $1 ~ /Status/ && $2 ~ /=/ { Status[ID] = $3 ;
                            StatusInformation[ID] = $0 ; gsub(/^.*= /,"",StatusInformation[ID])
                          }
Section == "hostbusadapter" && $1 ~ /Name/ && $2 ~ /=/ { Name[ID] = $0 ; gsub(/^.*= /,"",Name[ID])
#Attaching to Array One
                                          AttachedTo[ID] = Name["Array_1"]
                                        }


END { for (ID in Name) {
#Clean Up
          if ( Type[ID] == "Voltage" && Name[ID] ~ /Power/ ) { Type[ID] = "Power" }
          if ( Value[ID] == "NA" ) { Value[ID] = ""}
#Print
          if ( Name[ID] != "" ) {
              if ( Type[ID] == "Enclosure" ) {print "MSHW;" ID ";" Type[ID] ";" Name[ID] ";" Value[ID] ";" Status[ID] ";" StatusInformation[ID] ";" Model ";" Configuration ";" SystemVersion ";" }
                  else {print "MSHW;" ID ";" Type[ID] ";" Name[ID] ";" Value[ID] ";" Status[ID] ";" StatusInformation[ID] ";" Location[ID] ";" AttachedTo[ID] ";"}
              }
          }
    }