# syscli --getstatus commoncomponent --storagearray
$0 ~ /Component Name =/ { AttachedTo = $NF}

# syscli --getstatus networkport
$1 ~ /Name/ && $2 ~ /=/ {deviceID = $NF}
$1 ~ /Value/ && $2 ~ /=/ { Speed[deviceID] = $3 ;
                           if ( Speed[deviceID] ~ /NA/ ) { Speed[deviceID] = ""}
                           if ( $4 ~ /[Gg][Bb]/ ) { Speed[deviceID] = Speed[deviceID] * 1000 }
                         }
$1 ~ /Status/ && $2 ~ /=/ { LinkStatus[deviceID] = $3 }

# syscli --show netcfg
/^eth[0-9][0-9]* |^vmnic[0-9][0-9]* |^em[0-9][0-9]* |^[Pp][0-9][0-9]*[Pp][0-9][0-9]* / {
    deviceID = $1 ; MAC[deviceID] = $NF ;
}
/^ +RX packets:/ {
    receivePackets[deviceID] = substr($2, 9, length($2) - 8)
    receiveErrors[deviceID] = substr($3, 8, length($3) - 7)
}
/^ +TX packets:/ {
    transmitPackets[deviceID] = substr($2, 9, length($2) - 8)
    transmitErrors[deviceID] = substr($3, 8, length($3) - 7)
}
/^ +RX bytes:.*TX bytes:/ {
    receiveBytes[deviceID] = substr($2, 7, length($2) - 6)
    transmitBytes[deviceID] = substr($6, 7, length($6) - 6)
}

END { for (deviceID in Speed) {
      print "MSHW;" deviceID ";" Speed[deviceID] ";" LinkStatus[deviceID] ";" receivePackets[deviceID] ";" transmitPackets[deviceID]";" receiveErrors[deviceID] + transmitErrors[deviceID] ";" receiveBytes[deviceID] ";" transmitBytes[deviceID] ";" MAC[deviceID] ";MAC;" AttachedTo ";"
      }
    }